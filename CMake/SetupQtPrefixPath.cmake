if(WIN32)
    # use registry to find Qt https://stackoverflow.com/questions/15639781/how-to-find-the-qt5-cmake-module-on-windows
    get_filename_component(_QT_BIN [HKEY_CURRENT_USER\\Software\\Classes\\Applications\\QtProject.QtCreator.qml\\shell\\Open\\Command] PATH)
    string(REPLACE "//" "/" _QT_BIN "${_QT_BIN}")
    string(REPLACE "/Tools" ";" _QT_BIN "${_QT_BIN}")
    list(GET _QT_BIN 0 _QT_BIN)

    file(GLOB _QT_VERSIONS "${_QT_BIN}/5.*")
    list(SORT _QT_VERSIONS)
    list(REVERSE _QT_VERSIONS)

    if(DEFINED MSVC_TOOLSET_VERSION)
        set(_MSVC_YEAR)
        if(MSVC_TOOLSET_VERSION EQUAL 120)
            set(_MSVC_YEAR 2013)
        elseif(MSVC_TOOLSET_VERSION EQUAL 140)
            set(_MSVC_YEAR 2015)
        elseif(MSVC_TOOLSET_VERSION EQUAL 141)
            set(_MSVC_YEAR 2017)
        endif()

        set(_PLATFORM)
        if(CMAKE_CL_64)
            set(_PLATFORM _64)
        endif()

        foreach(_QT_VERSION ${_QT_VERSIONS})
            set(_PATH "${_QT_VERSION}/msvc${_MSVC_YEAR}${_PLATFORM}/lib/cmake/Qt5/")
            if(EXISTS ${_PATH})
                list(APPEND CMAKE_PREFIX_PATH ${_PATH})
            endif()
        endforeach()
    endif()
endif()
